&ACCESS RV
&REL 31
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF WIZJA_POMIAR( )

INT I
BOOL CZYSC

INTERRUPT OFF 1

CZYSC=STRCLEAR(POLECENIE[])
;Zmiana programu dla konkretnej tasmy
POLECENIE[]="PW,IN,0"
PRZYGOTUJ()
OTWORZ()
WYSLIJ()
WAIT FOR $DATA_SER3<>0
ODBIERZ()
ZAMKNIJ()

CZYSC=STRCLEAR(POLECENIE[])
;Pomiar polozenia obiektu
POLECENIE[]="T1"
PRZYGOTUJ()
OTWORZ()
WYSLIJ()
WAIT FOR $DATA_SER3<>0
ODBIERZ()
ZAMKNIJ()
PRZYPISZ()

IF WYNIK==0 THEN
  
;Przypisanie wspolrzednych punktu
  PUNKT_ZAPISZ=PUNKT_ZERO
  PUNKT_ZAPISZ.X=X_ODCZYT*SKALA-OFFSET_X
  PUNKT_ZAPISZ.Y=Y_ODCZYT*SKALA-DROGA_OBIEKTU+OFFSET_Y
  PUNKT_ZAPISZ.A=KAT_ODCZYT+90
  PUNKT_ZAPISZ.Z=-WYS_WYJSCIOWA
  
;Zapamietywanie polozenia wykrytych obiektow
  FOR I=1 TO 10
    IF PUNKT_REF[I].X==0 THEN
      PUNKT_REF[I]=PUNKT_ZAPISZ
      $TIMER[I]=0
      $TIMER_STOP[I]=FALSE
      EXIT
    ENDIF
  ENDFOR
  
ENDIF

INTERRUPT ON 1

END

GLOBAL DEF PRZYGOTUJ( )

BOOL CZYSC
CZYSC=STRCLEAR(WYJSCIE[])

OFFSET=0
SWRITE(WYJSCIE[], S_STATE, OFFSET, "%s%1r", POLECENIE[], CR)

IF S_STATE.RET1<>#CMD_OK THEN
  HALT
ENDIF

END

GLOBAL DEF OTWORZ( )

COPEN(:SER_3, HANDLE)
IF (HANDLE==0) THEN
  HALT
ENDIF

END

GLOBAL DEF WYSLIJ( )

TX_MODE=#SYNC
CWRITE(HANDLE, TX_STATE, TX_MODE, "%s", WYJSCIE[])
IF (TX_STATE.RET1<>#CMD_OK) THEN
  HALT
ENDIF

END

GLOBAL DEF ODBIERZ( )

OFFSET=0
TIMEOUT=5.0
RX_MODE=#ABS
CREAD(HANDLE, RX_STATE, RX_MODE, TIMEOUT, OFFSET, "%s", REZULTAT[])

END

GLOBAL DEF ZAMKNIJ( )

CCLOSE(HANDLE, CLOSE_STATE)
IF (CLOSE_STATE.RET1<>#CMD_OK) THEN
  HALT
ENDIF

END

DEF PRZYPISZ( )

OFFSET=0
SREAD(REZULTAT[], S_STATE, OFFSET, "%3s%1d%1r%10f%1r%10f%1r%8f", PREFIX[], WYNIK, COMMA, X_ODCZYT, COMMA, Y_ODCZYT, COMMA, KAT_ODCZYT)

END